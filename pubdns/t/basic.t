#!/usr/bin/env perl

use warnings;
use strict;

use Test::More tests => 4;
use File::Temp qw(tempfile);
use FindBin;

my $script = "$FindBin::Bin/../pubdns.pl";

# Sample NDP table output: host1 and host2 have both ULA and public addresses;
# host3 has only a ULA address and should be absent from the output.
my $ndp_output = <<'END';
Neighbor                               Linklayer Address  Netif Expire    St Flgs Prbs
fd42:dead:beef::1                      aa:bb:cc:dd:ee:01  em0   permanent R
2001:db8::1                            aa:bb:cc:dd:ee:01  em0   permanent R
fd42:dead:beef::2                      aa:bb:cc:dd:ee:02  em0   permanent R
2001:db8::2                            aa:bb:cc:dd:ee:02  em0   permanent R
fd42:dead:beef::3                      aa:bb:cc:dd:ee:03  em0   permanent R
END

my $zone_input = <<'END';
$TTL 3600
@ SOA ns1.example.com. admin.example.com. 2024010101 3600 900 604800 300
$ORIGIN example.com.
host1   AAAA    fd42:dead:beef::1
host2   AAAA    fd42:dead:beef::2
host3   AAAA    fd42:dead:beef::3
END

my $expected_output = <<'END';
;; Generated by pubdns.pl
;; Do not modify directly.

$TTL 3600

@ SOA ns1.example.com. admin.example.com. 2024010101 3600 900 604800 300

host1	AAAA	2001:0db8:0000:0000:0000:0000:0000:0001
host2	AAAA	2001:0db8:0000:0000:0000:0000:0000:0002
END

my ( $ndp_fh, $ndp_file )   = tempfile( UNLINK => 1 );
my ( $zone_fh, $zone_file ) = tempfile( UNLINK => 1 );

print $ndp_fh $ndp_output;
close($ndp_fh);

print $zone_fh $zone_input;
close($zone_fh);

my $output =
  qx{perl "$script" --ndp-file "$ndp_file" "$zone_file"};

is( $?, 0, 'script exits successfully' );
like( $output, qr/^;; Generated by pubdns\.pl/m, 'output has generated header' );
unlike( $output, qr/host3/, 'host with no public NDP entry is omitted' );
is( $output, $expected_output, 'output matches expected zone records' );
