#!/usr/bin/env perl

use warnings;
use strict;

use Carp;
use Pod::Usage;
use Getopt::Long;
use Net::IP qw(:PROC);

my ( $help, $man, $prefix, $ndp_file );

GetOptions(
    'prefix|p=s'   => \$prefix,
    'ndp-file|n=s' => \$ndp_file,
    'man'          => \$man,
    'help'         => \$help,
) or croak "Incorrect usage!\n";

pod2usage( { -verbose => 2 } ) if $man;
pod2usage( { -verbose => 1 } ) if $help;

if ( @ARGV != 1 || !$prefix ) {
    pod2usage( { -verbose => 1, -exitval => 1 } );
}

my $input_file = $ARGV[0];
my $prefix_obj = new Net::IP($prefix) or croak( Net::IP::Error() );
my $origin     = "";

# Build NDP table from router: IPv6 -> MAC and MAC -> [IPv6, ...]
my %ip_to_mac;
my %mac_to_ips;

my $ndp_fh;
if ($ndp_file) {
    open( $ndp_fh, '<', $ndp_file ) or croak "Cannot open ndp file: $!";
} else {
    open( $ndp_fh, '-|', 'ndp', '-a' ) or croak "Cannot run ndp: $!";
}
while ( my $line = <$ndp_fh> ) {
    chomp $line;
    next if $line =~ /^Neighbor/;    # Skip header

    # Strip zone ID from link-local addresses (e.g., fe80::1%em0)
    $line =~ s/^(\S+?)%\S+(\s)/$1$2/;

    # Match lines with a valid MAC address in the linklayer column
    if ( $line =~ /^(\S+)\s+([0-9a-f]{2}(?::[0-9a-f]{2}){5})\s+/i ) {
        my ( $ip_str, $mac ) = ( $1, lc($2) );
        my $ip_obj = new Net::IP($ip_str) or next;
        my $ip_full = lc( $ip_obj->ip() );

        $ip_to_mac{$ip_full} = $mac;
        push @{ $mac_to_ips{$mac} }, $ip_full;
    }
}
close($ndp_fh);

open( my $input_fh, '<', $input_file ) or croak "Cannot open input file: $!";

print ";; Generated by pubdns.pl\n";
print ";; Do not modify directly.\n\n";

while ( my $line = <$input_fh> ) {
    chomp($line);

    if ( $line =~ /^\$ORIGIN\s+(\S+)/ ) {
        $origin = $1;
    }
    elsif ( $line =~ /^(\$TTL\s+\S+|\@\s+SOA\s)/ ) {

        # Copy default TTL and SOA record verbatim.
        print "$line\n\n";
    }
    elsif ( $line =~ /^\s*([^\s;]+)\s+AAAA\s+(\S+)/ ) {
        my ( $hostname, $ula ) = ( $1, $2 );
        my $ula_obj = new Net::IP($ula) or next;
        my $ula_full = lc( $ula_obj->ip() );

        my $mac = $ip_to_mac{$ula_full};
        next unless defined $mac;

        for my $cand_ip ( @{ $mac_to_ips{$mac} } ) {
            my $cand_obj = new Net::IP($cand_ip) or next;
            if ( $prefix_obj->overlaps($cand_obj) != $IP_NO_OVERLAP ) {
                print "$hostname\tAAAA\t$cand_ip\n";
                last;
            }
        }
    }
}

close($input_fh);

__END__

=head1 NAME

pubdns.pl - Generate a DNS zone file for public IPv6 addresses

=head1 SYNOPSIS

pubdns.pl --prefix 2001:db8::/32 foo.zone

pubdns.pl --man

=head1 DESCRIPTION

My home network primarily uses ULA addressing for IPv6 because Comcast doesn't
guarantee static public IPv6 prefix delegation.  Therefore, my main zone file
(and its generated reverse zone files) reference ULA addresses internally.

However, it's still sometimes useful to have forward and reverse DNS entries
for hosts' public IPv6 addresses.  For example, this allows tcpdump to show
hostnames instead of IP addresses.

This script takes as input a forward zone file and a known public IPv6 prefix.
It then uses the router's NDP table to cross-reference the input file's ULA
host definitions, by MAC address, with any known corresponding addresses under
the public prefix, outputting a forward zone file for the public prefix.

=head1 OPTIONS

=over 4

=item B<--prefix> I<prefix>, B<-p> I<prefix>

The public IPv6 prefix to generate records for (required).  Only addresses
within this prefix will be included in the output.

=item B<--ndp-file> I<file>, B<-n> I<file>

Read NDP table data from I<file> instead of running C<ndp -a>.  Intended for
testing.

=item B<--help>

Print a brief help message and exit.

=item B<--man>

Print the full manual page and exit.

=back

=head1 ARGUMENTS

=over 4

=item I<zonefile>

Path to the input forward zone file containing ULA AAAA records.

=back

=cut
